# $Id: PKGBUILD 174822 2013-01-07 18:49:23Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xdg-utils
_oldver=1.1.0
_gitver=050333e34e2fe7430ad2ead37e5a0420ac0c2a3e # xdg-mime does not search mimeinfo.cache
_gitdate=20121008
pkgver=${_oldver}.git${_gitdate}
pkgrel=2
pkgdesc="Command line tools that assist applications with a variety of desktop integration tasks"
arch=('any')
url="http://portland.freedesktop.org/"
license=('MIT')
depends=('sh' 'xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl' 'lynx' 'xmlto')
optdepends=('kdebase-runtime: for KDE support in xdg-open'
            'libgnome: for GNOME support in xdg-open'
            'exo: for Xfce support in xdg-open'
            'xorg-xprop: for Xfce support in xdg-open'
            'pcmanfm: for LXDE support in xdg-open'
            'perl-file-mimeinfo: for generic support in xdg-open')
source=(#http://cgit.freedesktop.org/xdg/xdg-utils/snapshot/xdg-utils-${_gitver}.tar.gz
        ftp://ftp.archlinux.org/other/$pkgname/xdg-utils-${_gitver}.tar.gz
        eval_browser.patch)

sha1sums=('a48eac12b86a8c52ad0537ad671b751bffbd97fd'
          'f7029a87076b3375bb41b00a5687a613db2259f4')

build() {
	# Use newer version of xdg-open script straight from git repo
	#cd ${srcdir}
	#git clone git://anongit.freedesktop.org/xdg/${pkgname} "${pkgname}-git"
	#cp "${pkgname}-git"/scripts/xdg-open{,.in} ${srcdir}/${pkgname}-${_gitver}/scripts/

	cd ${srcdir}/${pkgname}-${_gitver}

	# eval with '$var' doesn't work if BROWSER variable contains ' chars
	#patch -p0 -i "${srcdir}"/eval_browser.patch

	./configure --prefix=/usr --mandir=/usr/share/man
	make scripts-clean -C scripts # needed if it's a git snapshot
	make man scripts -C scripts # needed if it's a git snapshot
#	make # for release builds
}

package() {
	cd ${srcdir}/${pkgname}-${_gitver}
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
	install -dm755 $pkgdir/usr/share/desktop-directories

	# xdg-open is horrible piece of ****
	rm -f ${pkgdir}/usr/bin/xdg-open
}
