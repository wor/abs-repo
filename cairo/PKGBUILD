# $Id: PKGBUILD 148684 2012-02-05 11:46:48Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo
pkgver=1.10.2
pkgrel=3
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'fontconfig' 'pixman' 'glib2' 'sh')
options=('!libtool')
source=(http://cairographics.org/releases/${pkgname}-${pkgver}.tar.gz
        cairo-respect-fontconfig.patch
        cairo-1.10.0-buggy_gradients.patch)
sha1sums=('ccce5ae03f99c505db97c286a0c9a90a926d3c6e'
          '8b843a9934e5112b6188e5bcf4adfc1fdaf9fa04')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"
  #https://bugs.freedesktop.org/show_bug.cgi?id=11838
  patch -Np1 -i "${srcdir}/cairo-respect-fontconfig.patch"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  #make check || /bin/true # 248 Passed, 65 Failed [2 crashed, 8 expected], 28 Skipped
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
sha1sums=('ccce5ae03f99c505db97c286a0c9a90a926d3c6e'
          '76fdca5bf0a803a37869f725adbedd9f0691671d'
          '8b843a9934e5112b6188e5bcf4adfc1fdaf9fa04')
