# $Id: PKGBUILD 64046 2012-02-08 23:19:32Z lcarlier $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.2.0beta4
_realpkgver=1.2.0-beta4
pkgrel=1
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl' 'icu' 'fontconfig' 'lzo2' 'hicolor-icon-theme' 'desktop-file-utils')
install=openttd.install
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset'
            'openttd-nullsfx: allow playing without opensfx'
            'timidity++: midi music'
            'timidity-freepats: midi music (instruments)')
source=("http://binaries.openttd.org/releases/${_realpkgver}/${pkgname}-${_realpkgver}-source.tar.xz"
ttd_data.tar.gz)

build() {
  cd ${pkgname}-${_realpkgver}

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir=${pkgdir} \
    --doc-dir=share/doc/${pkgname} \
    --menu-name="OpenTTD" \
    --personal-dir=.${pkgname}

  make
}

package() {
  cd ${pkgname}-${_realpkgver}

  make install
  cd $pkgdir/usr/share/openttd
  tar --no-overwrite-dir -xzf $srcdir/ttd_data.tar.gz
}

md5sums=('41c18120a3f0d77c8f3ca11e2b6730e7'
         '9e9795c5007540791520bd3fd0387411')
