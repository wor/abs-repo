# $Id: PKGBUILD 142638 2011-11-12 17:15:35Z ibiru $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd
pkgver=0.16.5
pkgrel=2
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://mpd.wikia.com/wiki/Server"
depends=('libao' 'ffmpeg' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite3' 'jack' 'libmms' 'wavpack' 'libmpcdec' 'avahi' 'libid3tag'
         'libpulse')
makedepends=('pkgconfig' 'doxygen')
install=${pkgname}.install
backup=(etc/conf.d/mpd)
changelog=ChangeLog
source=("http://downloads.sourceforge.net/musicpd/${pkgname}-${pkgver}.tar.bz2"
'mpd' 'mpd.conf.d')
md5sums=('f7564cff12035f6a1112cce770655df7'
         '7af1ed0a7e5f07b23bbc5f347abe841d'
         'bd3167f7d2f5fe3442748e027755a615')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-lastfm \
    --enable-jack \
    --enable-pulse \
    --enable-documentation \
    --disable-libwrap \
    --disable-cue \
    --disable-sidplay \
    --with-systemdsystemunitdir=/lib/systemd/system

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # set our dirs in mpd.conf file
  sed -i 's|^music_directory.*$|#music_directory "path_to_your_music_collection"|1' doc/mpdconf.example
  sed -i 's|playlist_directory.*$|playlist_directory "/var/lib/mpd/playlists"|1' doc/mpdconf.example
  sed -i 's|db_file.*$|db_file "/var/lib/mpd/mpd.db"|1' doc/mpdconf.example
  sed -i 's|log_file.*$|log_file "/var/log/mpd/mpd.log"|1' doc/mpdconf.example
  sed -i 's|error_file.*$|error_file "/var/log/mpd/mpd.error"|1' doc/mpdconf.example
  sed -i 's|#pid_file.*$|pid_file "/var/run/mpd/mpd.pid"|1' doc/mpdconf.example
  sed -i 's|#state_file.*$|state_file "/var/lib/mpd/mpdstate"|1' doc/mpdconf.example
  sed -i 's|#user.*$|user "mpd"|1' doc/mpdconf.example

  install -Dm644 doc/mpdconf.example ${pkgdir}/usr/share/mpd/mpd.conf.example

  install -D -m644 ${srcdir}/mpd.conf.d ${pkgdir}/etc/conf.d/mpd
  install -Dm755 "$srcdir/mpd" "$pkgdir/etc/rc.d/mpd"
  install -d ${pkgdir}/var/{lib/mpd/playlists,log/mpd}
}
